buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'io.github.robwin:jgitflow-gradle-plugin:0.3.0'
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

group = 'io.github.robwin'
description = 'A JGitflow Gradle Plugin.'

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.jfrog.bintray'
apply from: 'gradle/publishing.gradle'
apply plugin: 'io.github.robwin.jgitflow'
apply plugin: "com.gradle.plugin-publish"

tasks.withType(GroovyCompile) {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'external.atlassian.jgitflow:jgit-flow-core:1.0-m5.1'
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
}

initJGitflow{
    versiontag = 'v'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.coveralls {
    dependsOn 'check'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

pluginBundle {
    website = 'https://github.com/RobWin/jgitflow-gradle-plugin'
    vcsUrl = 'https://github.com/RobWin/jgitflow-gradle-plugin'
    description = project.description
    tags = ['gitflow']

    plugins {
        swagger2MarkupPlugin {
            id = 'io.github.robwin.jgitflow'
            displayName = project.description
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}